0 ONUU+1GOTO3700,1200:PRINT">> ERROR, LINE 0":STOP
1 SYSXF:ON1ANDQT$=""GOTO13:GOSUB2:ONEXGOTO1:RETURN
2 ONASC(QT$)-C7GOTO7,4,30,2400,810,8,2600,500,3600,2700,3170,16
3 PRINT"{home:2}{down}{rvrs on}>> ERROR, LINE 3":SYSXX:STOP
4 SYSFZ:A=ASC(QT$)+1:CF=FI(A):IFCF=0THENSYSCS:GOSUB2800
5 PRINTFM$(CF)
6 POKEIC,CF:QT$=FX$(CF):SYSRZ:CE=CE(CF):POKEIE,CE:POKESX,SX(CF):WI=0:RETURN
7 SYSXF+21:TW=TI:QT$=E5$:SYSZS
8 PRINT#CF,QT$+CR$;:TW=TI:POKEIC,CE:SYSIR:POKEIC,CF:EV=VAL(QS$)
9 QT$=CHR$(EV):SYSRZ:IFWITHENRETURN
11 WI=1:WP=1:WP(CF)=1:PRINTRV$MO$"WRITING TO DISK        "
12 RETURN:REM END OF SELECT/WRITE SECTOR,TIMEOUT FOLLOWS
13 IFPEEK(KP)=SVTHENIFWP=0THENGOSUB400:IFEX<>1THENRETURN
14 IFWPTHENIFTI-TW>20THENGOSUB3200
15 GOTO1
16 IX=-ABS(IX)-1:POKEEF,CF:RETURN
20 REM**** KEYBOARD INPUT
30 IFWPTHENGOSUB3200
40 IFPEEK(KP)=SVTHENGOSUB400:IFEX=2THENQT$=NS$:SYSRZ:RETURN
50 GETQS$:QS=ASC(QS$+NS$):ON1+QS/32GOTO100,90,80,70, 200,90,90,250
70 QT$=CHR$(QSAND31):SYSRZ:RETURN
80 IFULTHENQS$=CHR$(QSORB7)
90 SYSPA:QT$=QS$:SYSRZ:RETURN
100 REM**** UNSHIFTED SPECIALS
110 QT$=CHR$(QS)
120 IFQT$=CR$THENSYSRZ:RETURN
130 IFQT$=ES$THENSYSRZ:RETURN
140 IFQT$=CHR$(9)THENSYSRZ:RETURN
150 IFQT$=CHR$(20)THENQT$=CHR$(C7):SYSRZ:RETURN
160 IFQT$="{right}"THENQT$=CHR$(8):SYSRZ:RETURN
170 IFQT$="{down}"THENQT$=CHR$(10):SYSRZ:RETURN
180 GOTO30
200 REM**** SHIFTED SPECIALS
210 QT$=CHR$(QSANDC7)
220 IFQT$="{right}"THENSYSRZ:RETURN
230 IFQT$="{down}"THENSYSRZ:RETURN
240 GOTO120
250 REM**** PERFORM FUNCTION
260 QT$=NS$:GOSUB270:SYSRZ:RETURN
270 QS$=CHR$(QSAND63):REM MAKE EASIER TO READ
280 IFQS$="-"THENGOSUB3290:RETURN
290 IFQS$=":"THENGOSUB3490:RETURN
300 IFQS$="0"THENGOSUB3360:RETURN
310 IFQS$="1"THENGOSUB1100:RETURN
320 IFQS$="2"THENQT$="{$10}":POKETP,PEEK(TP)ORB7:IV=1:RETURN
330 IFQS$="3"THENGOSUB600:RETURN
340 IFQS$="4"THENUL=1-UL:PRINTRV$CO$"{right:2}"MID$(" {$d5}",UL+1,1):RETURN
350 RETURN
400 REM**** EXIT QUERY
410 I=0:QP$="{$07}":PRINTOF$MO$" ***** {$d1}{$d5}{$c9}{$d4}{$a0}{$c3}{$d0}/{$cd} ***** ":SYSSP:SYSSP+3
420 T=TI
430 GETA$:IFTI-T<130THEN430
440 QS$="{$c1}RE ALL FILES CLOSED?":GOSUB3000
450 IFA$<>"Y"ANDA$<>"{$d9}"THENRETURN
460 SYSSP+3:EX=2:PRINTRV$LEFT$(RB$,SW):TX=TI:RETURN
470 RETURN
500 REM**** SERVICE REQ
510 ONIVGOTO530
520 QT$=NS$:SYSRZ:RETURN
530 PRINTRV$MO$"CONTROL-P              "
540 T=TI:POKETP,PEEK(TP)ANDC7:QT$=CHR$(1):SYSRZ
550 IFTI-T<50THEN550
560 PRINTRV$MO$MF$
570 RETURN
600 REM**** PRINTER QUERY
610 QP$="":I=0:SYSSP:SYSSP+3:IFLI<0THENLI=PD
620 PRINTOF$MO$"{right} PRINT: ";:ONLI+1GOTO630,640,650,660,670,680
630 PRINT"ASCII SERIAL ":GOTO690
640 PRINT"ASCII IEEE   ":GOTO690
650 PRINT"CBM 2022/4022":GOTO690
660 PRINT"CBM 4022{$d0}    ":GOTO690
670 PRINT"CBM 8023     ":GOTO690
680 PRINT"CBM FRICTION ":GOTO690
690 T=TI
700 GETA$:IFA$<>""THEN740
710 IFTI-T<15THEN700
720 I=NOTI:PRINTOF$;:IFNOTITHENPRINTRV$;
730 PRINTMO$"{ctrl pound}"LEFT$(RT$,LEN(MF$)-2)"{$df}":GOTO690
740 IFA$="{rvrs on}"ORA$="{rvrs off}"ORA$=ES$ORA$=CR$ORA$="{stop}"THENPRINTRV$MO$MF$:SYSSP:RETURN
750 IFA$="D"ORA$="{$c4}"THENLI=PD:GOTO620
760 IFA$="{right}"ORA$="{left}"THENLI=LI-SGN(LI)+5*(1-SGN(LI)):GOTO620
770 LI=LI+1:IFLI>5THENLI=0
780 GOTO620
800 REM**** LIST DEVICE
810 IFLI<0THENQP$="{$07}":SYSSP:GOSUB600
820 SYSFZ:QT$=LEFT$(B5$,ASC(QT$)):SYSZS
830 ONLI+1GOTO840,900,930,940,950,1070:RETURN
840 REM ASCII SERIAL
850 QT$=QT$+""
860 FORI=1TOLEN(QT$)STEP16
870 IF(PEEK(59471)ANDCB)=0THEN870:REM PAUSE
880 QS$=MID$(QT$,I,16):SYSSE
890 NEXT:RETURN
900 REM ASCII IEEE
910 PRINT#127,QT$;:RETURN
920 REM CBM TRACTOR IEEE
930 S=2:GOTO960
940 S=3:GOTO960
950 S=1
960 QT$=""+QT$:SYSAP:IFRIGHT$(QT$,2)=PX$THEN980
970 PRINT#127,"{down}"LEFT$(QT$,LEN(QT$)-1);:RETURN
980 T=TI
990 IFTI-T<40THEN990
1000 PRINT#126,CHR$(2*S):PRINT#127
1010 T=TI
1020 IFTI-T<40THEN1020
1030 PRINT#126,CHR$(10*S):PRINT#127,"{down}"LEFT$(QT$,LEN(QT$)-1);
1040 T=TI
1050 IFTI-T<40THEN1050
1060 PRINT#126,CHR$(12*S):RETURN
1070 REM PETASCII IEEE
1080 QT$=""+QT$:SYSAP:PRINT#127,"{down}"LEFT$(QT$,LEN(QT$)-1);:RETURN
1100 REM**** LIST DEVICE C/R
1110 IFLI<0THENQP$="{$07}":SYSSP:GOSUB600
1120 ONLI+1GOTO1130,1140,1150,1150,1150
1130 QS$=CR$+CHR$(10):SYSSE:RETURN
1140 PRINT#127,CR$CHR$(10):RETURN
1150 PRINT#127,CR$;:RETURN
1200 REM**** CHECK IN, RUN, AND EXIT
1210 IFUU=0THENGOSUB3900
1220 UU=0:IFPEEK(145)=>176ANDPEEK(135)=96ANDPEEK(112)=230THEN1240
1230 MX$="KILL SOFTWARE OR POWER-RESET PET!":GOTO1430
1240 IFPEEK(24576)=76ANDPEEK(24579)=76THEN1260
1250 MX$="NO MCPACK CODE":GOTO1430
1260 POKE34813,(PEEK(59468)AND2)+12
1270 QC$="":POKE34814,PEEK(52):POKE34815,PEEK(53):SYS24576:A=VAL(QC$)
1280 IFA<>0.73THENMX$="MCPAC VERSION ERROR":SYS24576+9:GOTO1430
1290 POKE52,0:POKE53,64:CLR:GOSUB1500:GOSUB1800:OPEN127,PN,0:OPEN126,PN,6
1300 POKE59468,PEEK(59468)OR14:IFSW=>80THENGOSUB3340
1310 REM ## NOW START UP
1320 PRINT"{home:2}{clear}"RV$LEFT$(BB$,40)RV$MO$MF$RV$LEFT$(RT$,40);
1330 IFSW=>80THENPRINTLEFT$(" {$da}-{$d2}{$c1}{$cd}  1.74 (C) 1982 {$cd}ADISON {$c3}OMPUTER"+BB$,40)
1335 QP$=CHR$(10):SYSSP:PRINT"{home}{down:2}     {$da}-{$d2}{$c1}{$cd}  FROM  {$cd}ADISON {$c3}OMPUTER   "
1340 SYSSP+3:GOSUB2500
1350 SYSUS:SYSRC:QP$="":SYSSP:GOSUB1
1360 REM ## EXIT
1370 FORI=1TOFM:CLOSE(I):CLOSE64+I:NEXT:CLOSE127
1380 IFTI-TX<120THEN1380
1390 SYSSP+3:PRINT"{home:2}{clear}":POKE59468,SS:IFSW=>80THENGOSUB3340
1400 IFSW<80THENGOSUB3300
1410 IFBV=4THENPOKE212,UN
1420 POKE52,M1:POKE53,M2:SYSXX:CLR:END
1430 IFTI-TX<120THEN1430
1440 PRINT"{home:2}{clear}{down:4}{rvrs on} "MX$" ":CLR:END
1500 REM**** SET UP VARIABLES
1510 SS=PEEK(34813):QC$="":QR$="":QT$="":QS$="":QP$=""
1520 EX=1:RC=24576+3:CI=31800:M1=PEEK(34814):M2=PEEK(34815):WF=CI+2:BR=CI+3
1530 IC=CI+7:EF=CI+18:IE=EF+1:SX=IE+1:SE=RC+3:XX=SE+3:BK=XX+3:US=BK+3:CC=US+3
1540 AP=CC+3:PA=AP+3:IR=PA+6:SP=RC+33:XF=SP+9:FZ=XF+3:RZ=XF+6:ZS=XF+9:CS=XF+24
1550 CR$=CHR$(13):ES$=CHR$(27):NS$=CHR$(0):B7=128:C7=127:KP=166:C9=192:LI=-1
1560 RB$="{$a6}":FORI=1TO7:RB$=RB$+RB$:NEXT:RT$="{right}":FORI=1TO7:RT$=RT$+RT$:NEXT
1570 BB$=" ":FORI=1TO7:BB$=BB$+BB$:NEXT:NU$=CHR$(0):FORI=1TO7:NU$=NU$+NU$:NEXT
1580 E5$=CHR$(229):FORI=1TO7:E5$=E5$+E5$:NEXT:B5$=E5$+LEFT$(E5$,127)
1590 IB$=CHR$(242)+CHR$(0)+CHR$(2)+CHR$(26):RV$="{home}{rvrs on}":OF$="{home}{rvrs off}"
1600 MO$=LEFT$(RT$,14):MF$=LEFT$(BB$,23):CO$=LEFT$(RT$,9):PX$=CR$+NS$
1610 OPEN3,3:PRINT"{home:2}{$a0}":PRINT"{home}";:INPUT#3,A$:CLOSE3:SW=2*INT((LEN(A$)+1)/2)
1620 IFSW<80THENMF$=LEFT$("  {$da}-{$d2}{$c1}{$cd} (C) 1982 {$cd}.{$c3}.  ",23)
1640 GOSUB3850:SV=4:IFPEEK(57344)=76ANDPEEK(57347)=76THENSV=3
1650 QC$=CHR$(49)+CHR$(40)+CHR$(41)+CHR$(15)+CHR$(40)+CHR$(5)
1660 QC$=QC$+CHR$(25)+CHR$(33)+CHR$(0)+CHR$(7)
1670 SYSXF+27:QU$=QC$+""
1680 IFASC(MID$(QU$,10))>ASC(MID$(QU$,28))THENQU$=MID$(QU$,19)+LEFT$(QU$,18)
1690 IFBV=2ORASC(MID$(QU$,13))<>16ORASC(MID$(QU$,31))<>16THENQU$=""
1700 TP=65520:POKETP+3,0:POKETP+2,0:POKETP+3,46
1710 POKETP+1,4:POKETP,0:POKETP+1,0:POKETP,255:POKETP+1,4:POKETP,0
1720 DIMUC(31),FL$(31):FORI=4TO31:FL$(I)=CHR$(255):NEXT:CB=128:RETURN
1800 REM**** READ DEFAULTS
1810 OPEN65,UN,15:OPEN1,UN,2,"0:MCDEFS*,S,R"
1820 POKEIC,65:SYSIR:IFVAL(QS$)THEN2100
1830 POKEIC,129:POKEIC+1,13:SYSIR:IFVAL(QS$)<=0ORST<>4THEN2100
1840 DM=VAL(QS$):I=1
1850 SYSIR:IFST<>4ORLEN(QS$)<=2THEN2100
1860 IFLEFT$(QS$,1)="+"THENQS$=MID$(QS$,2)
1870 FI$(I)=LEFT$(QS$,LEN(QS$)-1)
1880 SYSIR:IFVAL(QS$)<=3ORVAL(QS$)=>31ORST<>4THEN2100
1890 UN(I)=VAL(QS$)
1900 SYSIR:IFVAL(QS$)<=0ORST<>4THEN2100
1910 UC(UN(I))=VAL(QS$):I=I+1:IFI<=DMTHEN1850
1920 SYSIR:IFST<>4ORVAL(QS$)<0ORVAL(QS$)>4THEN2100
1930 PD=VAL(QS$)
1940 SYSIR:IFST<>4OR(VAL(QS$)<=0ANDVAL(QS$)<>-1)ORVAL(QS$)>DMTHEN2100
1950 IB=VAL(QS$)
1960 SYSIR:A=VAL(QS$):IFST<>4THEN2100
1970 IFA=75THENA=0:GOTO2050
1980 IFA=110THENA=6:GOTO2050
1990 IFA=300THENA=12:GOTO2050
2000 IFA=600THENA=18:GOTO2050
2010 IFA=1200THENA=24:GOTO2050
2020 IFA=2400THENA=30:GOTO2050
2030 IFA=4800THENA=36:GOTO2050
2040 GOTO2100
2050 POKEBR,A
2055 POKECI,141
2060 SYSIR:IFST<>4ORVAL(QS$)<=2ORVAL(QS$)=>31THEN2100
2070 PN=VAL(QS$)
2080 SYSIR:IFST<>68ORQS$<>"ENDEFS"+CR$THEN2100
2090 CLOSE1:CLOSE65:GOSUB2300:RETURN
2100 CLOSE1:CLOSE65
2110 PRINT"{home:2}{clear}{down:4}{rvrs on} MCDEFS INVALID OR NOT FOUND "
2120 PRINT"{down}TYPE {rvrs on}{$d2}{$d5}{$ce}/{$d3}{$d4}{$cf}{$d0}{rvrs off} KEY TO QUIT"
2130 PRINT"TYPE ANY OTHER KEY FOR 2 DRIVES,
2140 PRINT"DRIVE A ON UNIT 8, DRIVE 0
2150 PRINT"DRIVE B ON UNIT 8, DRIVE 1
2160 GETA$:IFA$=""THEN2160
2170 IFA$="{stop}"THENPOKE52,M1:POKE53,M2:SYSXX:CLR:END
2180 PD=2:DM=2:FM=3:IB=-1:PN=4
2190 FI$(1)="0:CP/M DRIVE A":UN(1)=8
2200 FI$(2)="1:CP/M DRIVE B":UN(2)=8
2210 GOSUB2300:RETURN
2300 REM**** OPEN ERROR CHANNELS
2310 FORI=0TO31:UC(I)=0:NEXT
2320 FORI=1TODM:UC(UN(I))=UC(UN(I))+1:NEXT:REM BINGO THE UNITS
2330 FI=65
2340 FORI=3TO31
2350 IFUC(I)THENOPEN(FI),(I),15:UC(I)=FI:FI=FI+1
2360 NEXT
2370 FORI=1TODM:LE(I)=UC(UN(I)):NEXT
2380 RETURN
2400 REM**** READ SECTOR
2405 SYSXF+21
2410 IFCF=0THENQT$=E5$+CHR$(255):QS$="SELECT ERROR":SYSRZ:EV=1:RETURN
2420 SF=0:SYSIR:QT$=LEFT$(QS$,128):IFQT$="{$ff}"THENQT$=E5$
2430 IFLEN(QT$)<>128THENSF=255:QT$=E5$
2440 POKEIC,CE:SYSIR:POKEIC,CF:SF=VAL(QS$)OR SF:QT$=QT$+CHR$(SF)
2450 IFSF=0THENSYSRZ:IX=ABS(IX-SGN(IX)):POKEEF,SGN(IX)*CF:RETURN
2460 IFIX<0THENIX=10:FP=CF:A=DR(CF):GOTO2490
2470 IFVAL(QS$)=0THENQS$="SECTOR READ ERROR"
2480 QS$=QS$+"":GOSUB2750:SYSRZ:EV=1:RETURN
2490 GOSUB2750:GOSUB2800:PRINTFM$(CF):QT$="{$09}"+NS$:SYSXF+39:IFCF=0THEN2410
2495 PRINTRV$"{right:2}{back arrow}":QP$="{$07}":SYSSP:FORI=1TO70:NEXT:SYSSP:GOTO2410
2500 REM**** COLD BOOT
2510 POKECI+12,0:POKECI+13,5*16+6:POKECI+14,255:POKECI+15,5*16+11
2520 POKECI+16,0:POKECI+17,8*16+10:POKETP,23:SYSXF+18
2530 POKETP,16:POKE32768,195:POKE32769,0:POKE32770,234:POKETP,0
2540 FM$(0)=RV$+"{right:2}:???  ":FX$(0)=LEFT$(NU$,4):FM=3:REM MAX 3 OPEN
2600 REM**** WARM BOOT
2610 CLOSE120:GOSUB2700
2620 POKECI+12,0:POKECI+13,4*16:POKECI+14,255:POKECI+15,4*16+11
2630 POKECI+16,0:POKECI+17,9*16+4:POKETP,22:SYS XF+18
2640 POKECI+12,0:POKECI+13,4*16+12:POKECI+14,255:POKECI+15,5*16+5
2650 POKECI+16,0:POKECI+17,8*16:POKETP,23:SYS XF+18
2660 POKETP,64:RETURN
2700 REM**** CLOSE DISK
2710 PRINTRV$"{right}--    ":PRINTRV$MO$MF$
2720 SYSSP+3:SYSCS:FORI=1TOFM:CLOSE(I):DR(I)=0:WP(I)=0:NEXTI:WI=0:WP=0
2730 FORI=1TODM:FI(I)=0:NEXTI:FP=1:CF=0:POKEIC,CF:POKEEF,0:RETURN
2750 REM*** CLOSE BY INIT, NO MESSAGE
2760 X$="":A=DR(CF):FORI=1TODM:IFFI(I)=0ORUN(I)<>UN(A)THENNEXTI:GOTO2790
2770 FORX=1TOLEN(X$):IFMID$(X$,X,1)=LEFT$(FI$(I),1)THENX=999
2780 NEXTX:IFX<256THENX$=X$+LEFT$(FI$(I),1)
2785 NEXTI
2790 IFX$<>""THENPRINT#CE,"I"+LEFT$(X$,1):X$=MID$(X$,2):GOTO2790
2795 GOSUB2720:RETURN
2800 REM**** DISK REOPEN
2810 FI(DR(FP))=0:WP(FP)=0:CLOSE(FP):GOSUB3200:PRINTRV$"{right}"CHR$(C9+A)":... "
2820 IFA>DMTHENQS$=CHR$(C9+A)+": IS NOT IN MCDEFS":GOTO2920
2830 CF=FP:OPEN(CF),(UN(A)),(CF+1),FI$(A):SA(FP)=FP+1:CE=LE(A):CE(FP)=CE
2840 DR(CF)=A:FI(A)=CF:POKEIC,CE:SYSIR:EN$=QS$+"":IFVAL(QS$)THEN2920
2850 POKEIC,CF:SYSIR:A$=QS$+"":POKEIC,CE:SYSIR:IFVAL(QS$)THEN2920
2860 POKEIC,CF:SYSIR:B$=LEFT$(QS$,5):POKEIC,CE:SYSIR:IFVAL(QS$)THEN2920
2870 IFB$="8050S"THENIFFL$(UN(A))<>"8050S"THENGOSUB2940:ONEEGOTO2800,2930
2880 POKEIC,CF:FX$(CF)=LEFT$(A$,4):FM$(CF)=RV$+"{right}"+CHR$(C9+A)+":"+MID$(A$,5,5)
2890 IFLEN(A$)<9ORVAL(MID$(A$,5,5))<=0THENQS$="BAD CONTROL BLOCK":GOTO2920
2900 SX(CF)=ASC(MID$(FX$(CF),4)):POKEEF,FP:IX=8:FP=FP+1:IFFP>FMTHENFP=1
2910 RETURN
2920 PRINTFM$(0):GOSUB3000:CLOSE(FP):PRINTRV$"{right}      "
2930 SA(FP)=-1:DR(FP)=0:FI(A)=0:CF=0:GOSUB3200:RETURN
2940 EE=2:PRINT#CE,"M-R"CHR$(164)CHR$(67)CHR$(1):GET#CE,B$
2950 IFB$<>""ANDB$<>CHR$(255)THENCLOSE(FP):QS$="DISK UNIT ERROR":RETURN
2960 FL$(UN(A))="8050S":IFB$=""THENEE=0:RETURN
2970 PRINTRV$MO$MF$RV$MO$"8050 EXPAND":GOSUB2750:EE=1
2980 PRINT#CE,"M-W"CHR$(164)CHR$(67)CHR$(1)CHR$(0):PRINTRV$MO$MF$:RETURN
3000 REM**** SHOW AND FLASH A MESSAGE
3010 IX=10:POKEEF,CF:QS$=LEFT$("{$a0}"+QS$,22):I=0
3020 IFLEN(QS$)<5THEN3050
3030 FORI=5TOLEN(QS$):IFMID$(QS$,I,1)=","THENQS$=LEFT$(QS$,I-1)
3040 NEXT:QS$=QS$+" ":I=0:QP$="{$07}"
3050 PRINTOF$;:I=NOTI:IFITHENPRINTRV$;
3060 PRINTMO$QS$:IFQP$<>""THENSYSSP:SYSSP+3:QP$=""
3070 T=TI
3080 GETA$:IFA$<>""THENT=TI:GOTO3110
3090 IFTI-T<18THEN3080
3100 GOTO3050
3110 GETB$:IFPEEK(KP)<>255ORB$<>""THENT=TI
3120 IFTI-T<15THEN3110
3130 PRINTRV$MO$MF$:QP$="":SYSSP:RETURN
3170 REM**** MESSAGE AND TIMEOUT
3180 IFEV=0THENPOKEIC,CE:SYSIR:POKEIC,CF
3190 EV=0:PRINTRV$MO$MF$:GOSUB3000
3200 REM**** DFILE TIMEOUT
3210 WI=0:WP=0:FORI=1TOFM:IFWP(I)THENGOSUB3240
3220 NEXT:PRINTRV$MO$MF$:RETURN
3230 REM
3240 POKEIC,I:POKEIE,CE(I):QT$="{$01}"+NU$:SYSXF+39:SYSIR:WP(I)=0
3250 POKEIC,CE(I):SYSIR:POKEIC,CF:POKEIE,CE:IFVAL(QS$)THENGOSUB3000
3260 RETURN
3270 REM**** SPECIAL FNS
3280 REM ## ALT SCRUNCH
3290 IFSCTHEN3340
3300 REM ## SCRUNCH
3310 IFQU$<>""THENFORI=4TO9:POKE59520,I:POKE59521,ASC(MID$(QU$,I+1)):NEXT
3320 SC=1:RETURN
3330 REM ## UNSCRUNCH
3340 IFQU$<>""THENFORI=4TO9:POKE59520,I:POKE59521,ASC(MID$(QU$,I+19)):NEXT
3350 SC=0:RETURN
3360 REM ## ALT INV OF STATUS LINE
3370 IFRV$="{home}{rvrs on}"THEN3410
3380 REM ## RVS STATUS LINE
3390 RV$="{home}{rvrs on}":OF$="{home}{rvrs off}":FORI=32768TO32767+SW:POKEI,PEEK(I)ORB7:NEXT:RETURN
3400 REM ## NON RVS STATUS LINE
3410 RV$="{home}{rvrs off}":OF$="{home}{rvrs on}":FORI=32768TO32767+SW:POKEI,PEEK(I)ANDC7:NEXT:RETURN
3420 REM ## ALT FORE/BACK
3430 IFPEEK(CI+6)AND2THEN3470
3440 REM ## RVS BACK
3450 POKECI+6,PEEK(CI+6)OR2:QP$="":SYSSP:RETURN
3460 REM ## NORM BACK
3470 POKECI+6,PEEK(CI+6)AND253:QP$="":SYSSP:RETURN
3480 REM ## WS MODE
3490 IFSC=1ANDRV$="{home}{rvrs off}"THENIF(PEEK(CI+6)AND2)THEN3510
3500 GOSUB3300:GOSUB3400:GOSUB3450:RETURN
3510 GOSUB3330:GOSUB3390:GOSUB3470:RETURN
3600 REM**** REPLACE MCT
3610 SYSXF+30
3620 RETURN
3700 REM**** LOAD UP MACHINE CODE
3710 GOSUB3910:IFPEEK(145)=>176ANDPEEK(135)=96ANDPEEK(112)=230THEN3750
3720 MX$="{rvrs off}{left}SOFTWARE WHICH USES INTERRUPTS OR ADDS EXTRA"+CHR$(13)
3730 MX$=MX$+"COMMANDS TO BASIC, SUCH AS WEDGE, COMMAND-O,"+CHR$(13)
3740 MX$=MX$+"ETC., MUST BE KILLED BEFORE RUNNING CP/M.":GOTO1430
3750 TP=65520:POKETP+1,4:POKETP,0:POKETP+1,0:POKETP,255:POKETP+1,4
3760 POKETP,16:POKE32768,32:POKETP,0
3770 POKE24576,0:POKETP,8:POKE24576,251:POKETP,0:IFPEEK(24576)THEN3790
3780 POKETP,16:A=PEEK(32768):POKETP,0:IFA=251THEN3800
3790 MX$="NO Z-RAM CARD INSTALLED!":GOTO1430
3800 GOSUB3850:OPEN15,UN,15:OPEN1,UN,2,"0:MCPACK-*":INPUT#15,EN$:CLOSE15:CLOSE1
3810 IFLEFT$(EN$,2)<>"00"ORST<>0THENMX$="MCPACK NOT FOUND":GOTO1430
3820 POKE52,0:POKE53,128:CLR:UU=1:GOSUB3850
3830 IFBV=4THEN{$d6}"MCPACK-*",D0,U0+UN
3840 IFBV<>4THENLOAD"0:MCPACK-*",8
3850 REM**** BV/UNIT
3860 BV=2:IFPEEK(49152)=76THENBV=4
3870 UN=8:IFBV=4ANDPEEK(212)>3ANDPEEK(212)<16THENUN=PEEK(212)
3880 IFUN<4ORUN>31THENUN=8
3890 RETURN
3900 REM**** BANNER
3910 PRINT"{home:2}{clear}";:TX=TI:POKE32768,32:POKE33792,96:IFPEEK(32768)=96THEN3930
3920 PRINT"{right:20}{$0f}";
3930 PRINT"{home}{down:4}      {rvrs on} I/O SYSTEM BY PAUL SCHICK "
3940 PRINT    "{down} {rvrs on} COPYRIGHT (C) 1982 MADISON COMPUTER "
3950 PRINT    "{down}    {rvrs on} ALL RIGHTS RESERVED WORLDWIDE "
3960 PRINT    "{down}           {rvrs on} PLEASE STAND BY "
3970 RETURN
3980 REM**** END OF CODE
